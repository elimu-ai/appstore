import java.util.regex.Matcher
import java.util.regex.Pattern

plugins {
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}
apply plugin: 'com.android.application'

android {
    namespace 'ai.elimu.appstore'

    defaultConfig {
        applicationId "ai.elimu.appstore"
        minSdkVersion 26
        compileSdk 35
        targetSdkVersion 35
        versionCode 2005069
        versionName "2.5.69"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "${projectDir}/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.elimu.analytics // https://jitpack.io/#ai.elimu/analytics
    implementation libs.elimu.common.utils // https://jitpack.io/#ai.elimu/common-utils

    implementation libs.timber
    implementation libs.material

    // AndroidX
    implementation libs.androidx.appcompat
    implementation libs.androidx.recycler.view
    implementation libs.androidx.constraint.layout

    // Retrofit
    implementation libs.retrofit
    implementation libs.retrofit.gson

    // Room components
    implementation libs.androidx.room.runtime
    annotationProcessor libs.androidx.room.compiler
    androidTestImplementation libs.androidx.room.testing

    testImplementation libs.junit

    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso
}

publishing {
    publications {
        apk(MavenPublication) {
            artifact("${buildDir}/outputs/apk/release/app-release-unsigned.apk")
        }
    }
}
tasks.named("publishApkPublicationToMavenLocal") {
    mustRunAfter(":app:assembleRelease")
}

tasks.register('removeSnapshot') {
    doLast {
        println("removeSnapshot")

        def file = file("build.gradle")
        def originalFileContent = file.getText()

        Pattern pattern = Pattern.compile("versionName \"\\d+\\.\\d+\\.\\d+-SNAPSHOT\"")
        Matcher matcher = pattern.matcher(originalFileContent)
        matcher.find()
        println("match: ${matcher.group()}")

        def newVersionName = matcher.group().replace("-SNAPSHOT", "")
        println("newVersionName: ${newVersionName}")

        def newFileContent = originalFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+-SNAPSHOT\"", newVersionName)
        file.write(newFileContent)
    }
}
tasks.register('getVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}
tasks.register('bumpVersion') {
    doLast {
        println("bumpVersion")

        def currentVersionCode = android.defaultConfig.versionCode
        println("currentVersionCode: ${currentVersionCode}")

        def newVersionCode = currentVersionCode + 1
        println("newVersionCode: ${newVersionCode}")

        def newVersionName = newVersionCode.toString().substring(0, 1).toInteger() + "." + newVersionCode.toString().substring(1, 4).toInteger() + "." + newVersionCode.toString().substring(4, 7).toInteger()
        println("newVersionName: ${newVersionName}")

        def file = file("build.gradle")
        def originalFileContent = file.getText()
        def newFileContent = originalFileContent.replaceFirst("versionCode \\d+", "versionCode ${newVersionCode}")
        newFileContent = newFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+\"", "versionName \"${newVersionName}\"")
        file.write(newFileContent)
    }
}
tasks.register('addSnapshot') {
    doLast {
        println("addSnapshot")

        def file = file("build.gradle")
        def originalFileContent = file.getText()

        Pattern pattern = Pattern.compile("versionName \"\\d+\\.\\d+\\.\\d+\"")
        Matcher matcher = pattern.matcher(originalFileContent)
        matcher.find()
        println("match: ${matcher.group()}")

        def newVersionName = "${matcher.group().substring(12, matcher.group().length() - 1)}-SNAPSHOT\""
        println("newVersionName: ${newVersionName}")

        def newFileContent = originalFileContent.replaceFirst("versionName \"\\d+\\.\\d+\\.\\d+\"", "versionName ${newVersionName}")
        file.write(newFileContent)
    }
}

